buildscript {
    ext.kotlin_version = '1.3.31'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
    }
}

defaultTasks 'build'

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'

configurations {
    ktlint
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compile 'io.grpc:grpc-netty-shaded:1.21.0'
    compile 'io.grpc:grpc-protobuf:1.21.0'
    compile 'io.grpc:grpc-stub:1.21.0'
    compile 'javax.annotation:javax.annotation-api:1.3.2'
    
    ktlint "com.github.shyiko:ktlint:0.31.0"
}

repositories {
    jcenter()
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args["src/**/*.kt"]
}
check.dependsOn ktlint

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.7.1"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.21.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['build/generated/source/proto/main/java', 'build/generated/source/proto/main/grpc']
        }
    }
}